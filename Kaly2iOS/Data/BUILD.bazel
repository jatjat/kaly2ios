load("@build_bazel_rules_swift//swift:swift.bzl", "swift_library")
load("@build_bazel_rules_apple//apple:ios.bzl", "ios_unit_test")

grpc_stubs = [
    "Sources/Data/Protos/MapService.grpc.swift",
    "Sources/Data/Protos/RobotService.grpc.swift",
    "Sources/Data/Protos/SessionService.grpc.swift",
]

proto_models = [
    "Sources/Data/Protos/MapService.pb.swift",
    "Sources/Data/Protos/RobotService.pb.swift",
    "Sources/Data/Protos/SessionService.pb.swift",
]

tools = [
    "@protobuf//:protoc",
    "@grpc-swift-repo//:protoc-gen-grpc-swift",
    "@swift-protobuf-repo//:protoc-gen-swift",
]

protos = glob([
    "**/*.proto",
])

swift_library(
    name = "data",
    srcs = glob([
        "Sources/Data/Clients/**/*.swift",
        "Sources/Data/Repositories/**/*.swift",
        "Sources/Data/*.swift",
        "Sources/Data/Cleanse/*.swift",
    ]) + grpc_stubs + proto_models,
    module_name = "Data",
    visibility = ["//visibility:public"],
    deps = [
        "//Kaly2iOS/Domain:domain",
        "@cleanse-repo//:Cleanse",
        "@grpc-swift-repo//:GRPC",
    ],
    # data = [":grpc_stubs"]
)

genrule(
    name = "grpc_stubs",
    srcs = protos,
    outs = grpc_stubs,
    cmd = """
    $(location @protobuf//:protoc) \
    --plugin=$(location @grpc-swift-repo//:protoc-gen-grpc-swift) \
    --grpc-swift_out=Client=true,ExperimentalAsyncClient=true:$(GENDIR) \
    --proto_path=. $(SRCS)
    """,
    tools = tools,
)

genrule(
    name = "proto_models",
    srcs = protos,
    outs = proto_models,
    cmd = """
    $(location @protobuf//:protoc) \
    --plugin=$(location @swift-protobuf-repo//:protoc-gen-swift) \
    --swift_out=$(GENDIR) \
    --proto_path=. $(SRCS)
    """,
    tools = tools,
)

swift_library(
    name = "data-tests-lib",
    testonly = True,
    srcs = glob(["Tests/DataTests/**/*.swift"]),
    module_name = "DataTests",
    deps = [
        "data",
        "data-mocks",
        "//Kaly2iOS/Common:common",
        "@cleanse-repo//:Cleanse",
    ],
)

ios_unit_test(
    name = "data-tests",
    size = "medium",
    minimum_os_version = "15.0",
    visibility = ["//visibility:public"],
    deps = [":data-tests-lib"],
)

swift_library(
    name = "data-mocks",
    srcs = ["data-mocks-gen"],
    module_name = "DataMocks",
    visibility = ["//visibility:public"],
    deps = [
        "data",
        "@cleanse-repo//:Cleanse",
    ],
)

genrule(
    name = "data-mocks-gen",
    srcs = glob(["Sources/Data/**/*.swift"]),
    outs = ["Generated/GeneratedDataMocks.swift"],
    cmd = "mint run mockolo -srcs $(SRCS) -d $@ -i Data",
    visibility = ["//visibility:public"],
    # tools = ["@mockolo//:mockolo"],
)
