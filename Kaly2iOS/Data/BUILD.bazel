
load("@build_bazel_rules_swift//swift:swift.bzl", "swift_library")
load("@build_bazel_rules_apple//apple:ios.bzl", "ios_unit_test")

grpc_stubs = [
        "Sources/Data/Protos/MapService.grpc.swift",
        "Sources/Data/Protos/RobotService.grpc.swift",
        "Sources/Data/Protos/SessionService.grpc.swift"
      ]
proto_models = [
        "Sources/Data/Protos/MapService.pb.swift",
        "Sources/Data/Protos/RobotService.pb.swift",
        "Sources/Data/Protos/SessionService.pb.swift"
]
tools = ["@protobuf//:protoc", "@grpc-swift-repo//:protoc-gen-grpc-swift", "@swift-protobuf-repo//:protoc-gen-swift"]
protos = glob([
        "**/*.proto",
    ])

swift_library(
    name = "data",
    srcs = glob([
        "Sources/Data/Clients/**/*.swift",
        "Sources/Data/Repositories/**/*.swift",
        "Sources/Data/*.swift",
        "Sources/Data/Cleanse/*.swift"
    ]) + grpc_stubs + proto_models,
    visibility = ["//visibility:public"],
    module_name = "Data",
    deps = ["@grpc-swift-repo//:GRPC",
    "@cleanse-repo//:Cleanse",
    "//Kaly2iOS/Domain:domain"
    ],
    # data = [":grpc_stubs"]
)

genrule(
    name = "grpc_stubs",
    outs = grpc_stubs,
    cmd = """
    $(location @protobuf//:protoc) \
    --plugin=$(location @grpc-swift-repo//:protoc-gen-grpc-swift) \
    --grpc-swift_out=Client=true,ExperimentalAsyncClient=true:$(GENDIR) \
    --proto_path=. $(SRCS)
    """,
    srcs = protos,
    tools = tools,
)

genrule(
    name = "proto_models",
    outs = proto_models,
    cmd = """
    $(location @protobuf//:protoc) \
    --plugin=$(location @swift-protobuf-repo//:protoc-gen-swift) \
    --swift_out=$(GENDIR) \
    --proto_path=. $(SRCS)
    """,
    srcs = protos,
    tools = tools,
)


swift_library(
    name = "data-tests-lib",
    module_name = "DataTests",
    srcs = glob(["Tests/DataTests/**/*.swift"]),
    testonly = True,
    deps = [
            "//Kaly2iOS/Common:common",
            "data",
            "data-mocks",
            "@cleanse-repo//:Cleanse"
    ],
)

ios_unit_test(
    name = "data-tests",
    size = "medium",
    minimum_os_version = "15.0",
    visibility = ["//visibility:public"],
    deps = [":data-tests-lib"]
)

swift_library(
    name = "data-mocks",
    module_name = "DataMocks",
    srcs = ["data-mocks-gen"],
    visibility = ["//visibility:public"],
    deps = [
        "data",
        "@cleanse-repo//:Cleanse",
    ],
)
genrule(
    name = "data-mocks-gen",
    srcs = glob(["Sources/Data/**/*.swift"]),
    outs = ["Generated/GeneratedDataMocks.swift"],
    cmd = "mint run mockolo -srcs $(SRCS) -d $@ -i Data",
    visibility = ["//visibility:public"],
    # tools = ["@mockolo//:mockolo"],
)